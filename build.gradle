buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.2'
    }
}

import twitter4j.*
import java.io.*

apply plugin: 'forge'
apply plugin: 'curseforge'

version = "1710.1180.2.0"
group= "com.renkin42.renkin42swt" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Renkin42SWT"

minecraft {
    version = "1.7.10-10.13.0.1180"
    assetDir = "eclipse/assets"
}

curse {
	apiKey = curseforge_key //Located in gradle.properties, not sharing.
	projectId = "60999"
	changelog = """
	2.0 port to 1.7.10.
	"""
	releaseType = "release"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'README.txt'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'changelog':project.curse.changelog
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'README.txt'
    }
}

task twitter << {
	// Don't try running this. You'll need the twitter4j.properties file
	// To get the keys, go to dev.twitter.com and create a new app.
	// Template for twitter4j.properties: https://gist.github.com/seratch/4238807
	Twitter twitter = new TwitterFactory().getInstance()
	Status status = twitter.updateStatus("Stuff Worth Throwing " + project.curse.releaseType + " version " + project.version + " is now live on curse! Check it out at http://www.curse.com/mc-mods/minecraft/stuff-worth-throwing #Minecraft #Mods")
	println("Successfully updated the status to [" + status.getText() + "].")
}
